apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.aware.plugin.device_discovery" //EDIT ME
        minSdkVersion 18
        targetSdkVersion 25
        versionCode version_code
        versionName version_readable
    }
    packagingOptions {
        exclude 'META-INF/ECLIPSE_.SF'
        exclude 'META-INF/ECLIPSE_.RSA'
        exclude 'epl-v10.html'
        exclude 'edl-v10.html'
        exclude 'notice.html'
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }
}

if (System.getenv("storeFile") != null && System.getenv("storePassword") != null && System.getenv("keyAlias") != null && System.getenv("keyPassword") != null) {
    android.signingConfigs.release.storeFile = file(System.getenv("storeFile"))
    android.signingConfigs.release.storePassword = System.getenv("storePassword")
    android.signingConfigs.release.keyAlias = System.getenv("keyAlias")
    android.signingConfigs.release.keyPassword = System.getenv("keyPassword")
} else {
    println("No keystore defined. Unsigned!")
    android.buildTypes.release.signingConfig = null
}

repositories {
    maven { url 'https://jitpack.io' } //this allows library generation directly from GitHub source-code
    maven{
        url "https://repo.eclipse.org/content/repositories/californium/"
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$support_libs"
    compile "com.github.denzilferreira:aware-client:$aware_libs"
    compile 'org.eclipse.californium:californium-core:1.1.0-SNAPSHOT'
}
